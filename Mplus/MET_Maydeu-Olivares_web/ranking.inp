Title: career ranking data: Unrestricted model
DATA: FILE IS 'career ranking.dat';
! the data file contains 57 observations in free format
! the rankings have been transformed to paired comparisons

VARIABLE: 
	NAMES ARE pc12 pc13 pc14 pc23 pc24 pc34;
! the names reflect the comparisons	performed

	CATEGORICAL = pc12 pc13 pc14 pc23 pc24 pc34;
! the data is treated as categorical

ANALYSIS:
	TYPE = MEANSTRUCTURE;
! both thresholds and tetrachoric correlations will be modeled
		
	ESTIMATOR=WLSM;
! DWLS estimation with mean corrected S-B statistic
	
	PARAMETERIZATION = THETA;
! the program uses a model-based diagonal matrix D to enforce
! the variance standardization

MODEL:
	f1 BY pc12-pc14@1;
	f2 by pc23-pc24@1;
	f3 by      pc34@1;
	f2 by pc12@-1;
	f3 by pc13@-1 pc23@-1;
	f4 by pc14@-1 pc24@-1 pc34@-1;
	! this is matrix A, fixed factor loadings

! the factors are 
!		f1 = Academic
!		f2 = Clinical
!		f3 = Educational 
!		f4 = Industrial

	[pc12$1-pc34$1@0];
! intercepts fixed at zero

	[f1-f3* f4@0];
! the means of first n-1 factors are free, the last mean is fixed at 0

	pc12-pc34@0;
! pair specific error variances fixed at 0

! UNRESTRICTED MODEL SPECIFICATION
      f1@1;
      f4@1;
! factor variances for the first and last factors are fixed at 1
! all other factor variances are free parameters
      f2 with f1*;
      f3 with f1-f2*;
! factor covariances free except those involving the last object
      f4 with f1-f3@0;
! which are fixed at 0

! CASE 3 MODEL SPECIFICATION
!	f1-f3*;
!	f4@1;
! factor variances are free except for the last one, fixed at 1
!	f2 with f1@0;
!	f3 with f1-f2@0;
!	f4 with f1-f3@0;
! factor covariances fixed at 0

! CASE 5 MODEL SPECIFICATION
!	f1-f4@1;
! factor variances are fixed at 1
!	f2 with f1@0;
!	f3 with f1-f2@0;
!	f4 with f1-f3@0;
! factor covariances fixed at 0

OUTPUT: TECH1; TECH5;
! use TECH1 to verify that the A matrix is properly specified
! use TECH5 to obtain the function minimum (needed for S-B nested tests)

