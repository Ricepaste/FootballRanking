Title: cars pc data: two factor model
DATA: 
	FILE IS '\Users\sharpaste\Documents\program\
	testing\Mplus\MET_Maydeu-Olivares_web\cars_pc.dat';

VARIABLE: NAMES ARE pc12 pc13 pc14 pc15 pc16 pc17 pc23 pc24 pc25 
pc26 pc27 pc34 pc35 pc36 pc37 pc45 pc46 pc47 pc56 pc57 pc67 ;
	CATEGORICAL = pc12 pc13 pc14 pc15 pc16 pc17 pc23 pc24 pc25 
pc26 pc27 pc34 pc35 pc36 pc37 pc45 pc46 pc47 pc56 pc57 pc67 ;
! the data is treated as categorical

ANALYSIS:TYPE = MEANSTRUCTURE;
! both thresholds and tetrachoric correlations will be modeled
		ESTIMATOR=WLSM;
! DWLS estimation with mean corrected S-B statistic
! for mean and variance corrected S-B use WLSMV instead
		PARAMETERIZATION = THETA;
! the program will use the model based diagonal matrix D

MODEL:
	AX    by pc12-pc17@1;
	punto by pc23-pc27@1;
	micra by pc34-pc37@1;
	corsa by pc45-pc47@1;
	P106  by pc56-pc57@1;
	ibiza by      pc67@1;
	
	punto by pc12@-1;
	micra by pc13@-1 pc23@-1;
	corsa by pc14@-1 pc24@-1 pc34@-1;
	P106  by pc15@-1 pc25@-1 pc35@-1 pc45@-1;
	ibiza by pc16@-1 pc26@-1 pc36@-1 pc46@-1 pc56@-1;
	polo  by pc17@-1 pc27@-1 pc37@-1 pc47@-1 pc57@-1 pc67@-1;
! This is the A matrix
! first order factor loadings fixed
! the first order factors are named after the objects
!		AX    = Citroen AX
!		punto = Fiat Punto
!		micra = Nissan Micra
!		corsa = Opel Corsa
!		P106  = Peugeot 106
!		ibiza = Seat Ibiza
!		polo  = Volkswagen Polo

	[pc12$1-pc67$1@0];
! intercepts fixed at zero

	pc12-pc67(1)*.1;
! pair specific error variances are common, starting value of .1

! FACTOR MODEL SPECIFICATION

! f1 and f2 are the Thurstonian factors (=  second order factors)
	f1 by AX-ibiza*.5;
	f1 by polo@0;

	f2 by AX@0;
	f2 by punto-ibiza*.3;
	f2 by polo@0;
! factor loading matrix: lower triangular and
! factor loadings for last object fixed at zero
! starting value of .5 and .3

	AX-ibiza(2)*.5;
! equal variances of unique factors with starting value of .5
! to obtain variances that are not equal to each other use instead
!	AX-ibiza*.5;
	polo@1;
! variance for the last object fixed at 1

	f1 with f2@0;
! factors are uncorrelated
	f1-f2@1;
! variance of the factors fixed at 1

	[AX-ibiza];
! means of the first order factors are free, the last mean is fixed at 0

! if these means are to depend on the factor means use instead of above
!	[AX-ibiza](3);
! common intercept for all object, except for the last one, 
! which is fixed at zero
! and declare the factor means free using
!	[f1 f2];

OUTPUT: TECH1; TECH5;
! use TECH1 to verify that the model is properly specified
! use TECH5 to obtain the function minimum (needed for S-B nested tests)

